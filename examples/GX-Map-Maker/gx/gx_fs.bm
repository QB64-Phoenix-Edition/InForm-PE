' GX - A BASIC Game Engine for QB64
' 2021 boxgaming - https://github.com/boxgaming/gx
'

$INCLUDEONCE

'$INCLUDE: 'gx_str.bm'

' File System Utility Methods
' -------------------------------------------------------------------------
FUNCTION GXFS_GetFilename$ (filepath AS STRING)
    DIM s AS STRING, i AS INTEGER
    s = filepath
    s = GXSTR_Replace(s, "\", "/")
    i = _INSTRREV(s, "/")
    s = MID$(s, i + 1)
    GXFS_GetFilename = s
END FUNCTION

FUNCTION GXFS_GetParentPath$ (filepath AS STRING)
    DIM s AS STRING, i AS INTEGER
    s = filepath
    s = GXSTR_Replace(s, "\", "/")
    i = _INSTRREV(s, "/")
    s = MID$(s, 1, i - 1)
    s = GXSTR_Replace(s, "/", GXFS_PathSeparator)
    'IF GXFS_IsDriveLetter(s) THEN s = s + __GXFS_PathSeparator
    IF s = "" THEN s = GXFS_PathSeparator
    GXFS_GetParentPath = s
END FUNCTION

FUNCTION GXFS_GetFileExtension$ (filename AS STRING)
    DIM i AS INTEGER
    'i = __GX_StrLastIndexOf(filename, ".")
    i = _INSTRREV(filename, ".")
    GXFS_GetFileExtension = MID$(filename, i + 1)
END FUNCTION

FUNCTION GXFS_RemoveFileExtension$ (filename AS STRING)
    DIM i AS INTEGER
    i = _INSTRREV(filename, ".")
    GXFS_RemoveFileExtension = MID$(filename, 1, i - 1)
END FUNCTION

FUNCTION GXFS_IsDriveLetter (path AS STRING)
    IF LEN(path) = 2 AND RIGHT$(path, 1) = ":" THEN
        GXFS_IsDriveLetter = -1 'GX_TRUE
    ELSE
        GXFS_IsDriveLetter = 0 'GX_FALSE
    END IF
END FUNCTION

FUNCTION GXFS_PathSeparator$ ()
    $IF WINDOWS THEN
        GXFS_PathSeparator = "\"
    $ELSE
            GXFS_PathSeparator = "/"
    $END IF
END FUNCTION

FUNCTION GXFS_DirList& (path AS STRING, dirmode AS INTEGER, filenames() AS STRING)
    DIM count AS LONG
    DIM entryName AS STRING

    entryName = _FILES$(path)
    DO WHILE LEN(entryName)
        DIM lastChar AS STRING: lastChar = RIGHT$(entryName, 1)
        DIM isDir AS _BYTE: isDir = (lastChar = "\" OR lastChar = "/")

        IF (dirmode _ANDALSO isDir) _ORELSE (NOT dirmode _ANDALSO NOT isDir) THEN
            count = count + 1
            REDIM _PRESERVE filenames(0 TO count) AS STRING
            filenames(count) = entryName
        END IF

        entryName = _FILES$
    LOOP

    GXFS_DirList = count
END FUNCTION

FUNCTION GXFS_DriveList& (drives() AS STRING)
    $IF WINDOWS THEN
        DIM AS LONG count, driveCode
        DIM driveLetter AS STRING

        FOR driveCode = 65 TO 90 ' A-Z
            driveLetter = CHR$(driveCode) + ":"
            IF _DIREXISTS(driveLetter + "\") THEN
                count = count + 1
                REDIM _PRESERVE drives(0 TO count) AS STRING
                drives(count) = driveLetter
            END IF
        NEXT

        GXFS_DriveList = count
    $ELSE
        GXFS_DriveList = 0
    $END IF
END FUNCTION
